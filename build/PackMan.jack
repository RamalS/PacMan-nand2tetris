class PackMan
{
    field int  lastKey, direction;
    field bool updateSprite;
    field AnimationManager am;
    field Vector2 position;

    constructor PackMan new(int x, int y)
    {
        let lastKey = 0;
        let direction = 0;
        let updateSprite = true;
        let position = Vector2.new(x, y);
        let am = AnimationManager.new(2);

        do loadAnimations();

        return this;
    }

    method void loadAnimations()
    {
        var Animation animation;

        let animation = Animation.new(2);
        do animation.add(PackManAsset.mouthOpenLeft());
        do animation.add(PackManAsset.mouthClosed());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(PackManAsset.mouthOpenRight());
        do animation.add(PackManAsset.mouthClosed());
        do am.add(animation);

        do am.set(Constants.packManRight());

        return;
    }

    method void moveRight()
    {
        do position.setX(position.getX() + 1);
        let updateSprite = true;
        do am.set(Constants.packManRight());

        return;
    }

    method void moveLeft()
    {
        do position.setX(position.getX() - 1);
        let updateSprite = true;
        do am.set(Constants.packManLeft());

        return;
    }

    method void move()
    {  
        var int kybrd;
        
        let kybrd = Keyboard.keyPressed();
        //  do Logger.logInt(kybrd); 

        if ((kybrd = 130) & ~(lastKey = kybrd))
        {
            let direction = -1;
            let lastKey = kybrd;   
        } 
        if (kybrd = 132 & ~(lastKey = kybrd))
        {
            let direction = 1;
            let lastKey = kybrd;   
        }
        if (kybrd = 0 & ~(lastKey = kybrd))
        {
            let lastKey = kybrd;
        }

        if (direction = 1)
        {
            do moveRight();
        }
        if (direction = -1)
        {
           do moveLeft();
        }
          
        return;
    }

    method void draw()
    {
        var int location;

        if (updateSprite)
        {
            let location = ScreenManager.getLocation(position);

            if (direction = 1)
            {
                do ScreenManager.clearBlock(location - 1);
            }
            if (direction = -1)
            {
                do ScreenManager.clearBlock(location + 1);
            }

            do am.drawNext(location);

            let updateSprite = false;
        } 

        return; 
    }
}
