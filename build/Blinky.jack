class Blinky
{
    field int homeState;
    field AnimationManager am;
    field Vector2 scatterPoint;
    field Ghost ghost;

    constructor Blinky new(int x, int y)
    {
        let homeState = 0;
        let am = AnimationManager.new(6);
        let scatterPoint = Vector2.new(30, 1);

        do loadAnimations();

        let ghost = Ghost.new(x, y, am);
        do ghost.bindAnimations(Constants.blinkyUp(),
                                Constants.blinkyLeft(),
                                Constants.blinkyDown(),
                                Constants.blinkyRight(),
                                Constants.blinkyFrightened(),
                                Constants.blinkyDead());

        return this;
    }

    method void dispose()
    {
        do am.dispose();
        do scatterPoint.dispose();
        do ghost.dispose();
        do Memory.deAlloc(this);

        return;
    }

    method void loadAnimations()
    {
        var Animation animation;

        let animation = Animation.new(2);
        do animation.add(BlinkyAsset.right1());
        do animation.add(BlinkyAsset.right2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(BlinkyAsset.left1());
        do animation.add(BlinkyAsset.left2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(BlinkyAsset.down1());
        do animation.add(BlinkyAsset.down2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(BlinkyAsset.up1());
        do animation.add(BlinkyAsset.up2());
        do am.add(animation);

        let animation = Animation.new(1);
        do animation.add(GhostAsset.dead());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(GhostAsset.frightened1());
        do animation.add(GhostAsset.frightened2());
        do am.add(animation);

        do am.set(Constants.blinkyRight());

        return;
    }

    method void reset()
    {
        do ghost.reset();

        return;
    }

    method void resetPosition()
    {
        do ghost.resetPosition(Constants.blinkyRight());
        return;
    }

    method void checkIntersect()
    {
        do ghost.checkIntersect();
        return;
    }

    method void move()
    {
        if (~ghost.isReseted())
        {
            if (ghost.isDead())
                { do ghost.moveToHome(); }
            else {if (Game.isFrightened())
                { do ghost.frightenedState(); }
            else {if (Game.isScatter())
                { do ghost.moveToPoint(scatterPoint); }
            else
                { do ghost.moveToPoint(Game.pacManPosition()); }}}
        }
        else
            { do ghost.setReseted(false); }

        return;
    }

    method void draw()
    {
        do ghost.drawNext();
        return;
    }
}