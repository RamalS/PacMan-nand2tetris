class Inky
{
    field int homeState, pacManDirection;
    field AnimationManager am;
    field Vector2 scatterPoint, pointToGo, tempPoint;
    field Ghost ghost;

    constructor Inky new(int x, int y)
    {
        let homeState = 0;
        let am = AnimationManager.new(6);
        let scatterPoint = Vector2.new(1, 15);
        let pointToGo = Vector2.new(0, 0);
        let tempPoint = Vector2.new(0, 0);
        let pacManDirection = 1;

        do loadAnimations();

        let ghost = Ghost.new(x, y, am);
        do ghost.bindAnimations(Constants.inkyUp(),
                                Constants.inkyLeft(),
                                Constants.inkyDown(),
                                Constants.inkyRight(),
                                Constants.inkyFrightened(),
                                Constants.inkyDead());

        return this;
    }

    method void dispose()
    {
        do am.dispose();
        do scatterPoint.dispose();
        do ghost.dispose();
        do Memory.deAlloc(this);

        return;
    }

    method void loadAnimations()
    {
        var Animation animation;

        let animation = Animation.new(2);
        do animation.add(InkyAsset.right1());
        do animation.add(InkyAsset.right2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(InkyAsset.left1());
        do animation.add(InkyAsset.left2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(InkyAsset.down1());
        do animation.add(InkyAsset.down2());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(InkyAsset.up1());
        do animation.add(InkyAsset.up2());
        do am.add(animation);

        let animation = Animation.new(1);
        do animation.add(GhostAsset.dead());
        do am.add(animation);

        let animation = Animation.new(2);
        do animation.add(GhostAsset.frightened1());
        do animation.add(GhostAsset.frightened2());
        do am.add(animation);

        do am.set(Constants.inkyRight());

        return;
    }

    method void reset()
    {
        do ghost.reset();

        return;
    }

    method void resetPosition()
    {
        do ghost.resetPosition(Constants.inkyRight());
        return;
    }

    method void checkIntersect()
    {
        do ghost.checkIntersect();
        return;
    }

    method void move()
    {
        if (~ghost.isReseted())
        {
            if (ghost.isDead())
                { do ghost.moveToHome(); }
            else {if (Game.isFrightened())
                { do ghost.frightenedState(); }
            else {if (Game.isScatter())
                { do ghost.moveToPoint(scatterPoint); }
            else
            {
                do pointToGo.set(Game.pacManPosition());

                if (~(Game.pacManDirection() = 0))
                    { let pacManDirection = Game.pacManDirection(); }

                if (pacManDirection = 1)
                    { do pointToGo.setX(pointToGo.getX() + 2); }
                else {if(pacManDirection = -1)
                    { do pointToGo.setX(pointToGo.getX() - 2); }
                else {if(pacManDirection = 2)
                    { do pointToGo.setY(pointToGo.getY() + 2); }
                else
                {
                    do pointToGo.setY(pointToGo.getY() - 2);
                    do pointToGo.setX(pointToGo.getX() - 2);
                }}}

                do tempPoint.set(Game.blinkyGetPosition());
                do tempPoint.sub(pointToGo);
                do pointToGo.sub(tempPoint);

                do ghost.moveToPoint(pointToGo); 
            }}}
        }
        else
            { do ghost.setReseted(false); }

        return;
    }

    method void draw()
    {
        do ghost.drawNext();
        return;
    }
}