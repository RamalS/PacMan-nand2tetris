class MainMenu
{
    static int location, kybrd, lastKey, selectedMap, selectedDuration;
    static String pressSpace, map1, map2;
    static Animation pacManAnimation, pinkyAnimation, blinkyAnimation, inkyAnimation, clydeAnimation;

    function void init()
    {
        let location = ScreenManager.getLocation(12, 14);
        let kybrd = 0;
        let lastKey = 0;
        let selectedMap = 0;
        let selectedDuration = 5;
        let pressSpace = "PRESS SPACE!";
        let map1 = "MAP 1";
        let map2 = "MAP 2";

        let pacManAnimation = Animation.new(2);
        do pacManAnimation.add(PacManAsset.mouthOpenRight());
        do pacManAnimation.add(PacManAsset.mouthClosed());
        
        let pinkyAnimation = Animation.new(2);
        do pinkyAnimation.add(PinkyAsset.right1());
        do pinkyAnimation.add(PinkyAsset.right2());

        let blinkyAnimation = Animation.new(2);
        do blinkyAnimation.add(BlinkyAsset.right1());
        do blinkyAnimation.add(BlinkyAsset.right2());

        let inkyAnimation = Animation.new(2);
        do inkyAnimation.add(InkyAsset.right1());
        do inkyAnimation.add(InkyAsset.right2());

        let clydeAnimation = Animation.new(2);
        do clydeAnimation.add(ClydeAsset.right1());
        do clydeAnimation.add(ClydeAsset.right2());

        return;
    }

    function void draw()
    {
        do Screen.clearScreen();
        do ScreenAsset.pacManBaner(ScreenManager.getLocation(6, 2));

        do Output.moveCursor(10, 30);
        do Output.printString(map1);

        do Output.moveCursor(13, 30);
        do Output.printString(map2);
        
        do Output.moveCursor(18, 27);
        do Output.printString(pressSpace);
        

        let kybrd = 0;
        while (~(kybrd = 32))
        {
            let kybrd = Keyboard.keyPressed();

            if ((kybrd = 131) & ~(lastKey = kybrd))
            {
                let selectedMap = 0;
                let lastKey = kybrd;
            } 
            else {if (kybrd = 133 & ~(lastKey = kybrd))
            {
                let selectedMap = 1;
                let lastKey = kybrd;
            }
            else {if (kybrd = 0 & ~(lastKey = kybrd))
            {
                let lastKey = kybrd;
            }}}

            if (selectedDuration < 0)
            {
                if (selectedMap = 0)
                {
                    do ScreenManager.clearRow(Constants.getScreen() + 3584);
                    do Sys.wait(100);
                    do ScreenManager.clearBlock(ScreenManager.getLocation(14, 9));
                    do ScreenAsset.cursor(ScreenManager.getLocation(14, 7));
                    do Output.moveCursor(10, 30);
                    do Output.printString(map1);
                }
                else
                {
                    do ScreenManager.clearRow(Constants.getScreen() + 4608);
                    do ScreenManager.clearRow(Constants.getScreen() + 5120);
                    do Sys.wait(100);
                    do ScreenManager.clearBlock(ScreenManager.getLocation(14, 7));
                    do ScreenAsset.cursor(ScreenManager.getLocation(14, 9));
                    do Output.moveCursor(13, 30);
                    do Output.printString(map2);
                }

                let selectedDuration = 5;
            }

            let selectedDuration = selectedDuration - 1;

            do pinkyAnimation.next(location);
            do blinkyAnimation.next(location + 1);
            do inkyAnimation.next(location + 2);
            do clydeAnimation.next(location + 3);
            do pacManAnimation.next(location + 4);
            do MapAsset.dot(location + 5);
            do MapAsset.dot(location + 6);
            do MapAsset.dot(location + 7);

            do Sys.wait(100);
        }

        do Game.setSelectedMap(selectedMap);

        return;
    }
}