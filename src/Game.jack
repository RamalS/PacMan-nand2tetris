class Game
{
    static bool run;
    static int scatterState, factorTimer, drawTimer, moveTimer, scatterTimer, frightenedTimer, drawDuration, moveDuration, scatterDuration, frightenedDuration;

    static int score, highScore, dotsEaten, hp, level, totalDots, factor, scatterTimer, frightenedTimer, selectedMap;
    static bool scatter, frightened;
    static String scoreLabel, livesLabel, highScoreLabel, levelLabel;
    static PacMan pacMan;
    static Pinky pinky;
    static Blinky blinky;
    static Clyde clyde;
    static Inky inky;
    static Array map;

    function void init()
    {
        let map = ClassicMap.initMap();
        let pacMan = PacMan.new(0, 0);
        let pinky = Pinky.new(15, 7);
        let blinky = Blinky.new(16, 7);
        let inky = Inky.new(15, 8);
        let clyde = Clyde.new(16, 8);
        let scoreLabel = "Score: ";
        let livesLabel = "Lives: ";
        let highScoreLabel = "H. Score: ";
        let levelLabel = "Level: ";
        let level = 0;
        let scatterDuration = 2000;
        let frightenedDuration = 2000;
        let drawDuration = 20;
        let moveDuration = 40;
        let highScore = 0;

        return;
    }

    function void reset(bool resetScore)
    {
        var int i, j, index, pacManX, pacManY;

        do Memory.deAlloc(map);
        let map = Game.getMap();

        let i = 0;
        let j = 0;
        let totalDots = 0;

        while (i < 16)
        {
            if (j = 32)
            {
                let i = i + 1;
                let j = 0;
            }

            let index = i * 32 + j;

            if (Game.grid(index) = 1)
                { let totalDots = totalDots + 1; }

            if (Game.grid(index) = 4)
            {
                let pacManX = j;
                let pacManY = i;
            }

            let j = j + 1;
        }

        do pacMan.setDefaultPosition(pacManX, pacManY);

        if (resetScore)
        { 
            let score = 0;
            let factor = 80;
            let level = 0;
        }
        else
        {
            do Sys.wait(1000);
            do ScreenManager.clearScreenAnimated();
        }
        
        let hp = 3;
        let dotsEaten = 0;
        let scatterState = 0;
        let factorTimer = 0;
        let drawTimer = 0;
        let moveTimer = 0;
        let scatterTimer = 0;
        let frightenedTimer = 0;

        let run = true;
        let scatter = true;
        let frightened = false;

        do pinky.reset();
        do blinky.reset();
        do inky.reset();
        do clyde.reset();
        do pacMan.reset();
        do Map.draw(map);

        do Game.drawScore(2, 0);
        do Game.drawLevel(29, 0);
        do Game.drawLives(54, 0);

        return;
    }

    function void dispose()
    {
        do pacMan.dispose();
        do pinky.dispose();
        do blinky.dispose();
        do inky.dispose();
        do clyde.dispose();
        do Memory.deAlloc(map);

        return;
    }

    function void increaseScore(int index, int x)
    {
        let score = score + x;
        do Game.setGrid(index, 0);
        do Game.drawScore(2, 0);

        if (x = 1)
            { let dotsEaten = dotsEaten + 1; }

        if (dotsEaten = totalDots)
        {
            let dotsEaten = 0;
            let level = level + 1;
            do Game.drawLevel(29, 0);
            do Game.reset(false);

            if (factor > 5)
                { let factor = factor - 30; }
        }

        return;
    }

    function void decHP()
    {
        let hp = hp - 1;
        do pacMan.resetPosition();
        do clyde.resetPosition();
        do inky.resetPosition();
        do blinky.resetPosition();
        do pinky.resetPosition();
        do Game.drawLives(54, 0);

        if (Game.getHp() = 0)
            {
                do Game.setRun(false);
                if (score > highScore)
                    { let highScore = score; }
            }
       
        return;
    }

    function void drawScore(int x, int y)
    {
        do Output.moveCursor(y, x);
        do Output.printString(scoreLabel);
        do Output.printInt(score);

        return;
    }

    function void drawLevel(int x, int y)
    {
        do Output.moveCursor(y, x);
        do Output.printString(levelLabel);
        do Output.printInt(level);

        return;
    }

    function void drawLives(int x, int y)
    {
        do Output.moveCursor(y, x);
        do Output.printString(livesLabel);
        do Output.printInt(hp);

        return;
    }

    function void drawHighScore(int x, int y)
    {
        do Output.moveCursor(y, x);
        do Output.printString(highScoreLabel);
        do Output.printInt(highScore);

        return;
    }

    function int grid(int i)
    {
        var int x;
        let x = map[i];

        return x;
    }

    // Setters
    function void setSelectedMap(int selMap)
    {
        let selectedMap = selMap;

        return;
    }

    function void setRun(bool _run)
    {
        let run = _run;

        return;
    }

    function void setSeed()
    {
        do LCGRandom.setSeed(factor);

        return;
    }

    function void setGrid(int i, int x)
    {
        let map[i] = x;

        return;
    }

    function void setFrightened()
    {
        let frightened = true;

        return;
    }

    // Getters
    function int getHp() { return hp; }
    function Vector2 pacManPosition() { return pacMan.getPosition(); }
    function int pacManLocation() { return pacMan.getLocation(); }
    function int pacManGridIndex() { return pacMan.getGridIndex(); }
    function int pacManDirection() { return pacMan.getDirection(); }
    function int blinkyGetPosition() { return blinky.getPosition(); }
    function bool isScatter() { return scatter; }
    function bool isFrightened() { return frightened; }
    
    function Array getMap()
    {
        if (selectedMap = 0)
            { return ClassicMap.initMap(); }
        else
            { return ExoticMap.initMap(); }
    }

    function void loop()
    {
        do LCGRandom.setSeed(1234);

        while (run)
        {
            if (moveTimer > moveDuration)
            {
                do clyde.checkIntersect();
                do inky.checkIntersect();
                do blinky.checkIntersect();
                do pinky.checkIntersect();

                do clyde.move();
                do inky.move();
                do blinky.move();
                do pinky.move();
                do pacMan.move();
                let moveTimer = 0;
            }

            if (drawTimer > drawDuration)
            {
                do pacMan.draw();
                do pinky.draw();
                do blinky.draw();
                do inky.draw();
                do clyde.draw();
                let drawTimer = 0;
            }

            if (frightened)
            {
                if (frightenedTimer > frightenedDuration)
                {
                    let frightenedTimer = 0;
                    let frightened = false;
                }

                let frightenedTimer = frightenedTimer + 1;
            }
            else
                { let scatterTimer = scatterTimer + 1; }

            if (scatterTimer > scatterDuration)
            {
                if (scatterState < 7)
                {
                    let scatter = ~scatter;
                    let scatterTimer = 0;
                    let scatterState = scatterState + 1;
                }
            }

            do pacMan.input();

            let drawTimer = drawTimer + 1;
            let moveTimer = moveTimer + 1;
            let factorTimer = 0;

            while(factorTimer < factor)
                { let factorTimer = factorTimer + 1; }
        }

        return;
    }
}